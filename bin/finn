#!/usr/bin/env coffee

# dependencies
app				= require 'nomnom'

pkg				= require '../package.json'
config = {}





# run a command
run = (command, args) ->
	# load config
	try
		config = require "#{process.env.HOME}/.finn"
	for key, value of args
		config[key] = value

	# load command
	command = require "../scripts/#{command}"
	command config





# finn --version
app.option 'version',
	abbr: 'v'
	flag: true
	help: 'print finn version'
	callback: () ->
		console.log pkg.version
		process.exit 0





# finn server --wit … --adapter …
app.command 'server'
	.help 'run the finn server'

	.option 'wit',
		abbr: 'w'
		help: 'wit API token'

	.option 'adapter',
		abbr: 'a'
		help: 'adapter to use'

	.callback (args) ->
		run 'server', args





# parse arguments, call commands
app.parse()