#!/usr/bin/env coffee

# dependencies
app				= require 'nomnom'
fs				= require 'fs'
path			= require 'path'
pkg				= require path.join '..', 'package.json'
config = {}





# run a command
run = (command, args) ->
	# load config
	try
		config = require path.join args.resources, 'config'
	catch
		config = {}
	for key, value of args
		config[key] = value
	console.log config

	# load command
	command = require path.join '..', 'scripts', command
	command config





# geoffrey --version
app.option 'version',
	abbr: 'v'
	flag: true
	help: 'print geoffrey version'
	callback: () ->
		console.log pkg.version
		process.exit 0





# geoffrey server --wit … --ressources … --adapter …
app.command 'server'
	.help 'run the geoffrey server'

	.option 'resources',
		abbr: 'r'
		default: path.join process.env.HOME, '.geoffrey'
		help: 'path of config.json, plugins and adapters'

	.option 'wit',
		abbr: 'w'
		help: 'wit API token'

	.option 'adapter',
		abbr: 'a'
		default: config.adapter
		help: 'adapter to use'

	.callback (args) ->
		run 'server', args





# geoffrey deps --resources …
app.command 'deps'
	.help 'install script & adapter dependencies'

	.option 'resources',
		abbr: 'r'
		default: path.join process.env.HOME, '.geoffrey'
		help: 'path of config.json, plugins and adapters'

	.option 'clean',
		abbr: 'c'
		default: config.clean
		help: 'install dependencies from scratch'

	.callback (args) ->
		run 'deps', args





# geoffrey doctor
app.command 'doctor'
	.help 'vet the geoffrey installation'

	.callback (args) ->
		run 'doctor', args





# parse arguments, call commands
app.parse()